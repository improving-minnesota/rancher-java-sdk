apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

dependencies {
    compile "com.squareup.retrofit2:retrofit:2.0.0-beta3"
    compile "com.squareup.retrofit2:converter-jackson:2.0.0-beta3"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
}

publishing {
    publications {
        sdk(MavenPublication) {
            groupId 'io.rancher'
            artifactId 'rancher-java-sdk'
            version '1.0.0'

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile'}
            }

            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}