import io.rancher.RancherSchemaPlugin

plugins {
  id 'groovy'
  id 'maven'
  id 'signing'
  id 'codenarc'
}

apply plugin: RancherSchemaPlugin

repositories {
  mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.9:indy'
  compile 'com.squareup.retrofit2:retrofit:2.3.0'
  compile 'com.squareup.retrofit2:converter-jackson:2.3.0'
  compile 'org.slf4j:slf4j-api:1.7.25'
  compile 'ch.qos.logback:logback-classic:1.2.3'
  compile 'org.antlr:ST4:4.0.8'

  testCompile 'org.testcontainers:testcontainers:1.7.2'
  testCompile 'org.testcontainers:spock:1.7.2'
}

codenarc {
  maxPriority1Violations = 0
  maxPriority2Violations = 5
  maxPriority3Violations = 10
  configFile = rootProject.file('config/codenarc/codenarc.rules')
}

test {
  testLogging {
    showStandardStreams = true
    events 'passed', 'skipped', 'failed'
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Rancher API Client'
        url 'https://github.com/kod4n/rancher-client'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Client for interacting with the Rancher API'

        scm {
          url 'https://github.com/kod4n/rancher-client'
          connection 'scm:https://github.com/kod4n/rancher-client.git'
          developerConnection 'scm:git@github.com:kod4n/rancher-client.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'kod4n'
            name 'Andrew Allen'
            email 'kcod4n@gmail.com'
          }
        }
      }
    }
  }
}
